apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

ext {
    // Maven metadata
    publishedGroupId = 'com.romanpulov.library.msgraph'
    libraryName = 'library'
    // Save yourself a head ache, and set this equal to the name of the Android Studio library
    // module. The artifact name needs to match the name of the library.
    artifact = 'library'

    libraryDescription = 'Roman Pulov MSGraph library for Android'
    libraryVersion = '0.0.16'
}

android {
    namespace = "com.romanpulov.library.msgraph"

    defaultConfig {
        compileSdk 36
        minSdkVersion 24
        targetSdkVersion 36

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    publishing {
        singleVariant('release') {
        }
        singleVariant('debug') {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'com.android.volley:volley:1.2.1'

    // https://github.com/AzureAD/microsoft-authentication-library-for-android/issues/1864
    api('com.microsoft.identity.client:msal:5.4.0') {
        exclude(group : "io.opentelemetry")
    }
    implementation('io.opentelemetry:opentelemetry-api:1.40.0')
    implementation('io.opentelemetry:opentelemetry-context:1.40.0')

    implementation 'com.romanpulov:jutils-core:0.4.4'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.annotation:annotation:1.9.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = publishedGroupId
                artifactId = artifact
                version = libraryVersion
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = publishedGroupId
                artifactId = 'library-debug'
                version = libraryVersion
            }
        }
    }
}